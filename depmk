#!/bin/fish
#
# depmk - Install dependencies recursivly
# onodera, https://github.com/onodera-punpun

## EXECUTE

# Load library
source /usr/include/prtstuff

for flag in $argv
	switch $flag
		case -h --help
			echo -e 'Usage: depmk [options]\n'
			echo 'options:'
			echo '  -s,   --script          toggle execution of pre- and post-install'
			echo '  -r,   --readme          toggle opening of readmes'
			echo '  -h,   --help            print help and exit'
			exit 0
		case -s --script
			if test $script = true
				set script false
			else
				set script true
			end
		case -r --readme
			if test $readme = true
				set readme false
			else
				set readme true
			end
		case '*'
			echo 'Invalid option, use -h for help.' >/dev/stderr
			exit 1
	end
end

prtonlyroot 'install ports'

# Initialize values and get dependencies
set ports (depls)
set current 0
set total (echo $ports | wc -l)

for port in $ports
	cd $portdir/$port

	# Open REAMDE
	if test $readme = true
		if test -f ./README
			eval $EDITOR ./README
		end
	end

	# Execute pre-install
	if test $script = true
		if test -f ./pre-install
			./pre-install
			if test $status -eq 0
				echo "Executed $bright$port$foreground pre-install."
			else
				echo "Failed to execute $bright$port$foreground pre-install." >/dev/stderr
				exit 1
			end
		end
	end

	set current (math $current + 1)
	echo "Compiling port $current/$total, $bright$port$foreground."

	eval {$makecommand}
	if test $status -eq 0
		echo "Installed port $current/$total, $bright$port$foreground."
	else
		echo "Failed to install $bright$port$foreground." >/dev/stderr
		exit 1
	end

	# Execute post-install
	if test $script = true
		if test -f ./post-install
			./post-install

			if test $status -eq 0
				echo "Executed $bright$port$foreground post-install."
			else
				echo "Failed to execute $bright$port$foreground post-install." >/dev/stderr
				exit 1
			end
		end
	end
end
