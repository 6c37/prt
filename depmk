#!/bin/fish
#
# depmk - Install dependencies recursivly
# onodera, https://github.com/onodera-punpun

## CONFIGURATION

# Set config dir location
set configdir /etc/prtstuff


## FUNCTIONS

# This function reads the Pkgfile
function pkgfile
	if test -f ./Pkgfile
		cat ./Pkgfile
	else
		echo 'Pkgfile not found.' >/dev/stderr
		exit 1
	end
end


## EXECUTE

# Load config
source $configdir/config

if test (count $argv) -ge 1
	switch $argv
		case -h --help
			echo -e 'Usage: depmk [options]\n'
			echo 'options:'
			echo '  -s,   --script          toggle execution of pre- and post-install'
			echo '  -r,   --readme          toggle opening of readmes'
			echo '  -h,   --help            print help and exit'
			exit 0
		case -s --script
			if test $script = true
				set script false
			else
				set script true
			end
		case -r --readme
			if test $readme = true
				set readme false
			else
				set readme true
			end
		case '*'
			echo 'Invalid option, use -h for help.' >/dev/stderr
			exit 1
	end
end

for port in (depls)
	cd /usr/ports/$port

	# Open REAMDE
	if test $readme = true
		if test -f ./README
			eval $EDITOR ./README
		end
	end

	# Execute pre-install
	if test $script = true
		if test -f ./pre-install
			./pre-install
			if test $status -eq 0
				echo "Executed $port pre-install."
			else
				echo "Failed to execute $port pre-install." >/dev/stderr
				exit 1
			end
		end
	end

	# Make and install port
	eval {$makecommand}
	if test $status -eq 0
		echo "Installed $port."
	else
		echo "Failed to install $port." >/dev/stderr
		exit 1
	end

	# Execute post-install
	if test $script = true
		if test -f ./post-install
			./post-install

			if test $status -eq 0
				echo "Executed $port post-install."
			else
				echo "Failed to execute $port post-install." >/dev/stderr
				exit 1
			end
		end
	end
end
