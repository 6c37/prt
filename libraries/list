#!/bin/fish
#
# list - List ports
# onodera, https://github.com/onodera-punpun

## EXECUTE

# Load util
source /usr/share/prt/util

# Initialize default option values
set diff false
set installed false
set repo false
set version false

getopts $argv | while read -l opt val
	switch $opt
		case h help
			echo -e 'Usage: prt list [arguments]\n'
			echo 'arguments:'
			echo '  -d,   --diff            list installed ports that are out-of-date'
			echo '  -i,   --installed       list installed ports'
			echo '  -r,   --repo            list repo info as well'
			echo '  -v,   --version         list port version info as well'
			echo '  -h,   --help            print help and exit'
			exit 0
		case d diff
			set diff true
		case i installed
			set installed true
		case r repo
			set repo true
		case v version
			set version true
		case '*'
			echo 'Invalid argument, use -h for a list of arguments.' >/dev/stderr
			exit 1
	end
end

if test $diff = true
	set available (prt list -v)
	set installed (prt list -i -v)

	# TODO: Fix ports not found in port tree appearing
	for port in $installed
		set name (echo $port | cut -d ' ' -f 1)

		if not string match -q -r $port $available
			if string match -q -r $name $available
				if test $version = true
					if isatty stdout
						echo "$port$dark -> $foreground"(string match -r "^$name .*" $available | cut -d ' ' -f 2)
					else
						echo "$port -> $foreground"(string match -r "^$name .*" $available | cut -d ' ' -f 2)
					end
				else
					echo $name
				end
			end
		end
	end
else if test $installed = true
	if test $version = true
		grep -A 2 '^$' /var/lib/pkg/db | tr '\n' ' ' | string replace -r -a '\-\-' '\n' | string trim | head -n -1
	else
		grep -A 1 '^$' /var/lib/pkg/db | tr '\n' ' ' | string replace -r -a '\-\-' '\n' | string trim | head -n -1
	end
else
	if test $repo = true -a $version = true
		grep '^version=\|^release=' (find /usr/ports -type f -name 'Pkgfile' -print) | string replace "$prtdir/" '' | string replace -r '.*:release=' '-' | cut -d ' ' -f 1 | string trim | string replace '/Pkgfile:version=' ' ' | cut -d ' ' -f -2 | string trim | sed 'N;s/\n-/-/g'
	else if test $repo = true
		find $prtdir -mindepth 2 -maxdepth 2 -type d | string replace -r "$prtdir/" '' | sort
	else if test $version = true
		grep '^version=\|^release=' (find /usr/ports -type f -name 'Pkgfile' -print) | string replace -r "$prtdir/" '' | string replace -r '.*:release=' '-' | cut -d ' ' -f 1 | string trim | string replace '/Pkgfile:version=' ' ' | cut -d ' ' -f -2 | string trim | sed 'N;s/\n-/-/g' | cut -d '/' -f 2- | sort

	else
		find $prtdir -mindepth 2 -maxdepth 2 -type d | string replace -r "$prtdir/.*/" '' | sort | uniq
	end
end
