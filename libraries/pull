#!/bin/fish
#
# pull - Pull in ports
# onodera, https://github.com/onodera-punpun

## EXECUTE

# Load util
source /usr/share/prt/util

getopts $argv | while read -l opt val
	switch $opt
		case h help
			echo -e 'Usage: prt pull [arguments] [repos]\n'
			echo 'arguments:'
			echo '  -h,   --help            print help and exit'
			exit 0
		case '*'
			set repos $repos $val
	end
end

prtonlyroot 'pull in ports'

if test (count $repos) -ge 1
	set repos (find /etc/prt/pull -type f | string match -r '.*/'(string replace -a ' ' '$|.*/' "$repos")'$')
else
	set repos (find /etc/prt/pull -type f | sort)
end

# Initialize count values
set total (count $repos)
set current 0

for file in $repos
	source $file

	set current (math $current + 1)
	echo "Updating collection $current/$total, $bright$name$foreground."

	cd $portdir/$name >/dev/null
	if test $status -eq 0
		git checkout -q $branch
		git fetch -q --depth 1
		# TODO: Add move to sed
		# TODO: Use string
		git diff --pretty=format: --name-status $branch origin/$branch | sed "s/M\t/$dark-> "$foreground"Editing /g; s/A\t/$dark-> "$foreground"Adding /g; s/D\t/$dark-> "$foreground"Deleting /g" | sort
		git clean -q -f
		git reset -q --hard origin/$branch
	else
		git clone -q --depth 1 -b $branch $url $portdir/$name
		# TODO: Use string
		ls $portdir/$name | sed "s/^/$dark-> {$foreground}Adding /g"
	end
end