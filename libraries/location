#!/bin/fish
#
# location - Prints port location
# onodera, https://github.com/onodera-punpun

## EXECUTE

# Load util
source /usr/share/prt/util

# Initialize default option values
set duplicate false
set aliasing true

if test (count $argv) -ge 1
	# TODO: Only -n at the end works, geptops makes the next op a val..
	getopts $argv | while read -l opt val
		switch $opt
			case h help
				echo -e 'Usage: prt location [arguments] [ports]\n'
				echo 'arguments:'
				echo '  -d,   --duplicate       list duplicate ports as well'
				echo '  -n,   --no-alias        disable aliasing'
				echo '  -h,   --help            print help and exit'
				exit 0
			case d duplicate
				set duplicate true
			case n no-alias
				set aliasing false
			case '*'
				set ports $ports $val
		end
	end
else
	echo 'Please specify a port.' >/dev/stderr
	exit 1
end

# Get all ports
set portstree (prt list -r)

for port in $ports
	set location (string match -r ".*/$port\$" $portstree)

	# Order ports
	if test (count $location) -ge 2
		if test $duplicate = true
			set oldlocation $location
		end

		for repo in $order
			set locationrepo (string match -r "^$repo/.*" $location)

			if test -n "$locationrepo"
				set location $locationrepo
				break
			end
		end

	end

	# Alias ports
	if test $aliasing = true
		if string match -q -r "^$location=" $alias
			set location (string match -r "^$location=.*" $alias | cut -d '=' -f 2)
		end
	end

	# Finally print the location :)
	if test -n "$location"
		echo $location
	end

	# Print duplicate tree
	if test $duplicate = true
		if test (count $oldlocation) -ge 2
			set current 0

			for repo in $order
				set locationrepo (string match -r "^$repo/.*" $oldlocation)

				if test -n "$locationrepo"
					if test $current -ge 1
						if isatty stdout
							echo -n $dark
							printf '-> %.0s' (seq $current)
							echo -n $foreground
							echo $locationrepo
						else
							printf '-> %.0s' (seq $current)
							echo $locationrepo
						end
					end

					set current (math $current + 1)
				end
			end

			set -e oldlocation
		end
	end
end
