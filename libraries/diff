#!/bin/fish
#
# diff - List outdated packages
# onodera, https://github.com/onodera-punpun

## EXECUTE

# Load util
source /usr/share/prt/util

# Initialize default option values
set oldnew false

getopts $argv | while read -l opt val
	switch $opt
		case h help
			echo -e 'Usage: prt diff [arguments]\n'
			echo 'arguments:'
			echo '  -v,   --version         list installed and available version'
			echo '  -h,   --help            print help and exit'
			exit 0
		case v version
			set oldnew true
	end
end

# Get installed ports
set installed (prt list -i)

# Get versions and locations
set names (string replace -r ' .*' '' $installed)
set versions (string replace -r '.* ' '' $installed)
set locations (prt location $names)

for count in (seq (count $locations))
	set version $versions[$count]
	set available (prtpkgfile $locations[$count] | string match -r '^version=.*|^release=.*' | string replace -r '^version=|^release=' '' | string join '-')

	if test $version != $available
		if test $oldnew = true
			if prtpipe
				echo "$locations[$count] $version -> $available"
			else
				echo "$locations[$count] $version$dark -> $foreground$available"
			end
		else
			echo $locations[$count]
		end
	end
end
