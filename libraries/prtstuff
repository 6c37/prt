#!/bin/fish
#
# prtlib - Some common functions
# onodera, https://github.com/onodera-punpun

## FUNCTIONS

# The following declarations are needed:
# set ports (prtls)
#
# This function prints port locations
function prtlocation
	for port in $argv
		set location (string match -r ".*/$port\$" $ports)

		# Order ports
		if test (count $location) -ge 2
			for repo in $order
				set locationrepo (string match -r "^$repo/.*" $location)

				if test -n "$locationrepo"
					set location $locationrepo
					break
				end
			end
		end

		# Alias ports
		if string match -q -r "^$location=" $alias
			set location (string match -r "^$location=.*" $alias | cut -d '=' -f 2)
		end

		if test -n "$location"
			echo $location
		end
	end
end

# This function checks if the uses has root access
function prtonlyroot
	if test (id -u) -ge 1
		echo "Only root can $argv." >/dev/stderr
		exit 1
	end
end

# This function reads the Pkgfile
function prtpkgfile
	if test -z $argv
		if test -f $pkgfile
			cat ./Pkgfile
		else
			echo 'Pkgfile not found.' >/dev/stderr
			exit 1
		end
	else
		if test -f $argv/Pkgfile
			cat $argv/Pkgfile
		else
			echo "Pkgfile $bright$argv$foreground not found." >/dev/stderr
			exit 1
		end
	end
end


## Execute

# Load config
source /etc/prtstuff/config

# Load colors
set foreground (set_color $foreground)
set bright (set_color $bright)
set dark (set_color $dark)
