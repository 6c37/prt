#!/bin/fish
#
# prtl - Prints port location
# onodera, https://github.com/onodera-punpun

## FUNCTIONS

# This function prints port locations
function prtlocation
	set ports (prtls)

	for port in $argv
		set location (string match -r ".*/$port\$" $ports)

		# Order ports
		if test (count $location) -ge 2
			for repo in $order
				set locationrepo (string match -r "^$repo/.*" $location)

				if test -n "$locationrepo"
					set location $locationrepo
					break
				end
			end
		end

		# Alias ports
		if string match -q -r "^$location=" $alias
			set location (string match -r "^$location=.*" $alias | cut -d '=' -f 2)
		end

		if test -n "$location"
			echo $location
		end
	end
end


## EXECUTE

# Load library
source /usr/include/prtstuff

# Initialize default option values
set duplicate false

if test (count $argv) -ge 1
	for flag in $argv
		switch $flag
			case -h --help
				echo -e 'Usage: prtloc [options] [ports]\n'
				echo 'options:'
				echo '  -d,   --duplicate       list duplicate ports'
				echo '  -h,   --help            print help and exit'
				exit 0
			case -d --duplicate
				set duplicate true
			case '*'
				set ports $ports $flag
		end
	end
else
	echo 'Please specify a port.' >/dev/stderr
	exit 1
end

prtlocation $ports
