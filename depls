#!/bin/fish
#
# depls - List dependencies recursively
# onodera, https://github.com/onodera-punpun

## FUNCTIONS

# This function prints the Pkgfile dependencies
function prtdependencies
	string match -r '# Depends on:.*' $argv | string replace '# Depends on:' '' | string trim | string split ' ' | string trim -c ','
end

# This function prints port locations
# I don't use prtloc because it's to slow to prtls every iteration
# TODO: Use prtloc somehow?
function prtlocation
	for port in $argv
		set location (string match -r ".*/$port\$" $portstree)

		# Order ports
		if test (count $location) -ge 2
			for repo in $order
				set locationrepo (string match -r "^$repo/.*" $location)

				if test -n "$locationrepo"
					set location $locationrepo
					break
				end
			end

		end

		# Alias ports
		if test $aliasing = true
			if string match -q -r "^$location=" $alias
				set location (string match -r "^$location=.*" $alias | cut -d '=' -f 2)
			end
		end

		# Finally print the location :)
		if test -n "$location"
			echo $location
		end
	end
end

# This function prints dependencies recursivly
function prtrecursive
	for port in (prtlocation (prtdependencies (prtpkgfile $location)))
		# Continue if already checked
		if string match -q -r "^$port\$" $checked
			continue
		else
			set -g checked $port $checked
		end

		# Continue if already installed
		if test $all = false
			if string match -q -r "^"(echo $port | cut -d '/' -f 2)"\$" $installed
				continue
			end
		end

		# Print tree
		if test $tree = true
			if test $iteration -ge 1
				echo -n $dark
				printf '-> %.0s' (seq $iteration)
				echo -n $foreground
			end

			set -g iteration (math $iteration + 1)
		end

		# Finally print the port :)
		echo $port

		# Change location and loop again
		set -g location $port
		prtrecursive

		if test $tree = true
			set -g iteration (math $iteration - 1)
		end
	end
end


## EXECUTE

# Load library
source /usr/include/prtstuff

# Initialize default option values
set all false
set aliasing true
set tree false

getopts $argv | while read -l opt val
	switch $opt
		case h help
			echo -e 'Usage: depls [options]\n'
			echo 'options:'
			echo '  -a,   --all             also list installed dependencies'
			echo '  -n,   --no-alias        disable aliasing'
			echo '  -t,   --tree            list using tree view'
			echo '  -h,   --help            print help and exit'
			exit 0
		case a all
			set all true
		case n no-alias
			set aliasing false
		case t tree
			set tree true
			set iteration 0
		case '*'
			echo 'Invalid option, use -h for help.' >/dev/stderr
			exit 1
	end
end

# Get all and installed ports
set portstree (prtls)
set installed (prtls -i | cut -d ' ' -f 1)

prtrecursive
