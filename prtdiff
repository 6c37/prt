#!/bin/fish
#
# prtdiff - List installed ports with a different version available in the portstree
# onodera, https://github.com/onodera-punpun

## FUNCTIOS

# This function outputs the names of new ports
function prtdiff
	# Get installed ports
	set installed (prtls -i)

	# Get versions and locations
	set names (string replace -r ' .*' '' $installed)
	set versions (string replace -r '.* ' '' $installed)
	set locations (prtloc $names)

	for count in (seq (count $locations))
		set version $versions[$count]
		set available (prtpkgfile $locations[$count] | string match -r '^version=.*|^release=.*' | string replace -r '^version=|^release=' '' | string join '-')

		if test $version != $available
			if test $oldnew = true
				echo "$locations[$count] $version$dark -> $foreground$available"
			else
				echo $locations[$count]
			end
		end
	end
end


## EXECUTE

# Load library
source /usr/include/prtstuff

# Initialize default option values
set oldnew false

getopts $argv | while read -l opt val
	switch $opt
		case h help
			echo -e 'Usage: prtdiff [options]\n'
			echo 'options:'
			echo '  -v,   --version         list installed and available version'
			echo '  -h,   --help            print help and exit'
			exit 0
		case v version
			set oldnew true
	end
end

prtdiff $repos
