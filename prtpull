#!/bin/fish
#
# prtpull - Pull in ports using git
# onodera, https://github.com/onodera-punpun

## EXECUTE

# Load library
source /usr/include/prtstuff

# Initialize values
set current 0

getopts $argv | while read -l opt val
	switch $opt
		case h help
			echo -e 'Usage: prtpull [options] [repos]\n'
			echo 'options:'
			echo '  -h,   --help            print help and exit'
			exit 0
		case '*'
			set repos $repos $val
	end
end

prtonlyroot 'pull in ports'

if test (count $repos) -ge 1
	set repos (find /etc/prtstuff/pull -type f | string match -r '.*/'(string replace -a ' ' '$|.*/' "$repos")'$')
else
	set repos (find /etc/prtstuff/pull -type f | sort)
end

for file in $repos
	source $file

	set current (math $current + 1)
	set total (count $repos)
	echo "Updating collection $current/$total, $bright$name$foreground."

	cd $portdir/$name >/dev/null
	if test $status -eq 0
		git checkout -q $branch
		git fetch -q --depth 1
		git diff --pretty=format: --name-status $branch origin/$branch | sed "s/M\t/$dark-> "$foreground"Editing /g; s/A\t/$dark-> "$foreground"Creating /g; s/D\t/$dark-> "$foreground"Deleting /g" | sort
		git clean -q -f
		git reset -q --hard origin/$branch
	else
		git clone -q --depth 1 -b $branch $url $portdir/$name
		ls $portdir/$name | sed "s/^/$dark-> {$foreground}Creating /g"
	end
end